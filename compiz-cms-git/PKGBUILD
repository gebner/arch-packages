# Maintainer: Gabriel Ebner <gebner@gebner.org>
pkgname=compiz-cms
pkgver=0.5.r1.g6d924c4
pkgrel=1
pkgdesc="Color management plugin for compiz"
arch=('x86_64')
url="https://github.com/gebner/compiz-cms"
license=('GPL2')
depends=('compiz-ubuntu' 'lcms' 'colord')
makedepends=('cmake' 'boost')
install=compiz-cms.install
source=("$pkgname::git+https://github.com/gebner/compiz-cms.git")
md5sums=(SKIP)

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCOMPIZ_PLUGIN_INSTALL_TYPE=compiz -DCOMPIZ_DISABLE_SCHEMAS_INSTALL=yes
  make
}

package() {
  cd "$srcdir/$pkgname/build"
  make DESTDIR="$pkgdir/" install
}
